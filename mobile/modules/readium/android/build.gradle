apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

group = 'expo.modules.readium'
version = '0.2.0'

buildscript {
  def expoModulesCorePlugin = new File(project(":expo-modules-core").projectDir.absolutePath, "ExpoModulesCorePlugin.gradle")
  if (expoModulesCorePlugin.exists()) {
    apply from: expoModulesCorePlugin
    applyKotlinExpoModulesCorePlugin()
  }

  // Simple helper that allows the root project to override versions declared by this library.
  ext.safeExtGet = { prop, fallback ->
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
  }

  // Ensures backward compatibility
  ext.getKotlinVersion = {
    if (ext.has("kotlinVersion")) {
      ext.kotlinVersion()
    } else {
      ext.safeExtGet("kotlinVersion", "1.8.10")
    }
  }

  ext.readium_version = '2.4.0'

  repositories {
    jcenter()
    mavenCentral()
  }

  dependencies {
    classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${getKotlinVersion()}")
  }
}

afterEvaluate {
  publishing {
    publications {
      release(MavenPublication) {
        from components.release
      }
    }
    repositories {
      maven {
        url = mavenLocal().url
      }
    }
  }
}

android {
  compileSdkVersion safeExtGet("compileSdkVersion", 33)

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
  }

  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_17.majorVersion
  }

  namespace "expo.modules.readium"
  defaultConfig {
    minSdkVersion safeExtGet("minSdkVersion", 26)
    targetSdkVersion safeExtGet("targetSdkVersion", 33)
    versionCode 1
    versionName "0.2.0"
  }
  lintOptions {
    abortOnError false
  }
  publishing {
    singleVariant("release") {
      withSourcesJar()
    }
  }
}

repositories {
  jcenter()
  mavenCentral()
}

dependencies {
  implementation project(':expo-modules-core')
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${getKotlinVersion()}"
  implementation "org.readium.kotlin-toolkit:readium-shared:$readium_version"
  implementation "org.readium.kotlin-toolkit:readium-streamer:$readium_version"
  implementation "org.readium.kotlin-toolkit:readium-navigator:$readium_version"
  implementation "org.readium.kotlin-toolkit:readium-opds:$readium_version"
  implementation "org.readium.kotlin-toolkit:readium-lcp:$readium_version"
  implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.3'
  implementation 'androidx.fragment:fragment-ktx:1.6.2'
  implementation 'androidx.compose.ui:ui-android:1.5.4'
  implementation 'org.jsoup:jsoup:1.15.3'
  implementation 'com.mcxiaoke.koi:async:0.5.5'
  implementation 'com.mcxiaoke.koi:core:0.5.5'
}
